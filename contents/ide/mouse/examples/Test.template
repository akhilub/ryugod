"!Arithmetic Expressions: "
   777 223 + !   "="  3579 2579 - ! "="
   25 40 * !     "="  7000 7 / !    "="
   "1000!"
"Boolean Expressions: "
   #H,789 891 -; !   "=" #H,0 17 - 36 -; !  "="
   #Q,0,1; ! "="   #Q,0 1000 -,1000; !  "="
   #H,5 6 -; ! "=" #H,0 1000 - 1000 -; !    "="
   #H, #H,17 18 -; #Q,5,5; *; ! "="
   #H,#H,17 18 -; #H,5 6 -; +; ! "=0    "
   #H,1 0 -; ! "=" #H,0 0 1 - -; !   "="
   #Q,0,0; ! "=" #Q,1000,1000; ! "="
   #H,37 0 17 - -; ! "=" #H,1 0 -; ! "="
   #H,#H,18 17 -; #H,6 5 -; *; ! "="
   #H,#H,5 6 -; #Q,1,1; +; ! "=1!"
"Assignments: "
   A 1000 = A. ! "=" Z A. = Z. ! "="
   A 25 + . ! "=1000!"
"Conditional Statements: "
   1 [ "OK" ]
   0 [ " *** Error ***" ] "!"
"Loops: "
   N 0 =
   ( N. ! " " 10 N. - ^ N N. 1 + = )
   "!!   "
   M 0 =
   ( N 0 =
      ( M. N. * ! " "
         5 N. - ^ N N. 1 + = )
   "!   " 5 M. - ^ M M. 1 + = ) "!"
"Macro Calls: "
   " A: " #A; "!"
"Parameters: "
   " B: "  #B,"OK"; #B," OK"," *** Error ***";
   " C: "  #C,1,2,3,4; ! "=10"
   " D: "  #D,#D,1,2;,#D,3,4;; ! "=10!"
"Scope of Variables: "
   " E: "  #E,1000; "!"
"Recursive Macros: "
   " G: "  #G,4410; "!"
"End of tests.!"
$A "OK" @
$B %A @
$C %A %B %C %D + + + @
$D %A %B + @
$E X %A = X. #F,0; X. ! "=" ! @
$F X %A = @
$G N %A = F 2 =
   ( #R,N.,F.;  ^ F F. 1 + = )
   N. F. -
   [ F. ! " "
      #G,N. F. /; ] @
$H %A [1 @] 0 @
$Q %A %B - 1 + [%B %A - 1 + [1 @]] 0 @
$R %A %A %B / %B * - @
$$
